plugins {
    id 'war'
}

dependencies {
    implementation project(':nkim2061-backend')

    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'

    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.2.0'

    implementation 'org.slf4j:slf4j-api:2.0.3'
    runtimeOnly 'ch.qos.logback:logback-classic:1.4.3'
}

def deploymentName = 'idde-server'

def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")

task deploy(type: Copy, dependsOn: 'war') {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile

    rename war.archiveFileName.get(), "${deploymentName}.war"

    into "${tomcatHome}/webapps"
}


task undeploy(type: Delete) {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    delete "${tomcatHome}/webapps/${deploymentName}.war"
}